class:: SinOscFB
summary:: Feedback FM oscillator
related:: Classes/SinOsc, Classes/FSinOsc, Classes/PMOsc
categories::  UGens>Generators>Deterministic, UGens>Generators>Chaotic


Description::

SinOscFB is a sine oscillator that has phase modulation feedback; its output plugs back into the phase input.
Basically this allows a modulation between a sine wave and a sawtooth like wave. Overmodulation causes chaotic oscillation. It may be useful if you want to simulate feedback FM synths.




classmethods::

method::ar, kr

argument::freq

The base frequency of the sine oscillator in Hertz.

argument::feedback

The second argument is the amplitude of phase feedback in radians.

argument::mul

Output will be multiplied by this value.

argument::add

This value will be added to the output after any multiplication.


Examples::

code::


{SinOscFB.ar(440,MouseX.kr(0,4))*0.1}.play


{SinOscFB.ar(MouseY.kr(10,1000,'exponential'),MouseX.kr(0.5pi,pi))*0.1}.play


{SinOscFB.ar(100*SinOscFB.ar(MouseY.kr(1,1000,'exponential'))+200,MouseX.kr(0.5pi,pi))*0.1}.play


// Scope the wave form
{ SinOscFB.ar([400,301], MouseX.kr(0,4),0.3); }.scope;

//////////////////////
//////////////////////

{Mix.new(Array.fill(12, {Pan2.ar(SinOscFB.ar([60, 30, 120, 240, 480].choose, ChaosGen.allSubclasses.choose.ar(rrand(0.01, 20))), ChaosGen.allSubclasses.choose.ar(rrand(0.01, 10)))}))}.scope

//////////////////////
//////////////////////

({Mix.new(SinOsc.ar(Array.fib(rrand(5, 10), [1, 2, 4, 8, 16, 32, 64, 128].choose, [1, 2, 4, 8, 16, 32, 64, 128].choose), mul: SinOsc.kr([0.02, 0.0202], mul: 0.05))) !2}.scope; {Mix.new(Mix.fill(12, {SinOsc.ar(Array.fib(rrand(5, 10), [1, 3, 6, 9, 17, 33, 66, 129].choose, [1, 3, 6, 9, 17, 33, 66, 129].choose) * 33, mul: SinOsc.kr([0.02, 0.0202], mul: 0.025))})) !2}.scope; {(Mix.fill(12, {Pan2.ar(SinOscFB.ar([60, 30, 120, 240, 480].choose, ChaosGen.allSubclasses.choose.ar(rrand(0.01, 20))), ChaosGen.allSubclasses.choose.ar(rrand(0.01, 10)))}) * 0.03)}.scope;)

::
